#!/usr/local/bin/perl
;#
;# sdif: sdiff clone
;# by K. Utashiro <utashiro@sra.co.jp>
;# Original version on Jul 24 1991
;$rcsid = '$Id: sdif,v 1.6 1991/07/29 16:37:35 utashiro Exp $';
;#
;# Incompatibility:
;#	- has -n option
;#	- has -f option
;#	- no -o option
;#	- all tabs are expanded
;#	- default width is 80
;#	- rcs support
;#
;($myname = $0) =~ s,.*/,,;
;$usage = <<_;
Usage: $myname [-n] [-l] [-s] [-f] [-b] [-i] [-w] [-w #] file1 file2
       $myname [-rcs] [-q] [-rrev1 [-rrev2]] [$myname options] file
Options:
	-n	print line number
	-l	print left column only for identical line
	-s	don't print identical line
	-f	fold line instead of truncating
		(folded line is marked by '+' character)
	-b	ignore trailing blanks
	-i	ignore the case of letters
	-w	ignore all blanks
	-w #	specify width of output (default is 80)
	-rcs	compare rcs files
		(not required when revision number is supplied)
	-q	don't print rcs diagnostics
_
$opts = 'nlsfbiwhqdx';
while ($_ = $ARGV[0], /^-/ && shift) {
    next unless ($car, $cdr) = /^-?(.)(.*)/;
    if (/-rcs$/) { $rcs++; next; }
    if ($car eq 'r') {
	&usage("$_: Too many revisions\n") if (@rcs == 2);
	$rcs++; push(@rcs, $_); next;
    }
    if ($car eq 'w') {
	if ($cdr =~ /^\d+$/) { $width = $cdr; next; }
	if ($ARGV[$[] =~ /^\d+$/) { $width = shift; next; }
    }
    if (index($opts, "$car:") >= $[) {
	eval "\$opt_$car = length(\$cdr) ? \$cdr : \@ARGV ? shift : &usage";
	next;
    }
    if (index($opts, $car) >= $[) {
	eval "\$opt_$car++"; $_ = $cdr; redo;
    }
    &usage("Unknown option: $car\n\n");
}

$opt_h && &usage;
if ($rcs) {
    $rcsfile = shift || &usage("No RCS filename\n\n");
} else {
    &usage("Two filenames are required.\n\n") if (@ARGV != 2);
    ($ofile, $nfile) = splice(@ARGV, 0, 2);
}

sub usage {
    select(STDERR);
    print @_, $usage;
    print "$rcsid\n" if $rcsid =~ /:/;
    exit;
}

$width = 80 unless defined($width);
$width = int(($width - 5) / 2);
grep(eval "\$dopts .= '-$_ ' if \$opt_$_", 'b', 'i', 'w');

if ($rcs) {
    push(@rcsopt, '-q') if $opt_q;
    $diffcmd = "rcsdiff @rcsopt @rcs $dopts $rcsfile|";
    $ofile = "co @rcsopt -p " . shift(@rcs) . " $rcsfile|";
    if (@rcs) {
	$nfile = "co @rcsopt -p " . shift(@rcs) . " $rcsfile|";
    } else {
	$nfile = $rcsfile;
    }
    if ($opt_d) {
	print STDERR "\$ofile = $ofile\n";
	print STDERR "\$nfile = $nfile\n";
	print STDERR "\$diffcmd = $diffcmd\n";
    }
} else {
    $diffcmd = "diff $dopts $ofile $nfile |";
}

open(OLD, $ofile) || die "$ofile: $!\n";
open(NEW, $nfile) || die "$nfile: $!\n";
open(DIFF, $diffcmd) || die "cannot open diff: $!";

$format = "\%-${width}.${width}s \%s\%s\%s \%s\n";
$nformat = '%-4d %s';

while (<DIFF>) {
    if (($left, $ctrl, $right) = /([\d,]+)([adc])([\d,]+)/) {
	($l1, $l2) = &range($left);
	($r1, $r2) = &range($right);
	$identical_line = $l1 - $oline;
	$identical_line-- if ($ctrl ne 'a');
	&print_identical($identical_line);
	@old = @new = ();
	print if ($opt_d || $opt_s);
	if ($ctrl eq 'd' || $ctrl eq 'c') {
	    @old = &read(OLD, $n = $l2 - $l1 + 1);
	    &read(DIFF, $n);
	}
	&read(DIFF, 1) if ($ctrl eq 'c');
	if ($ctrl eq 'a' || $ctrl eq 'c') {
	    @new = &read(NEW, $n = $r2 - $r1 + 1);
	    &read(DIFF, $n);
	}
	&flush;
    } else {
	warn "Unrecognizable line -- $_";
    }
}
&print_identical(-1) unless $opt_s;
close(OLD); close(NEW);
close(DIFF);
exit (($? >> 8) == 2);

sub flush {
    while(@new || @old) {
	undef($old); undef($new);
	$mark = !@new ? '<' : @old ? '|' : '>';
	$new = shift(@new), ++$nline if (@new);
	$old = shift(@old), ++$oline if (@old);
	&out($old, $mark, $new);
    }
}

sub print_identical {
    local($n, $old, $new) = @_;
    while ($n--) {
	$old = <OLD>; $new = <NEW>;
	last if (!defined($old) && !defined($new));
	#last if (!defined($old = <OLD>) && !defined($new = <NEW>));
	$oline++; $nline++;
	next if $opt_s;
	if ($opt_l) {
	    $old = &num($oline, &expand($old)) if $opt_n;
	    print $old;
	} else {
	    &out($old, ' ', $new);
	}
    }
}

sub read {
    local($fh, $c, @buf) = @_;
    push(@buf, scalar(<$fh>)) while ($c--);
    @buf;
}

sub out {
    local($old, $mark, $new) = @_;
    local($o, $n);
    local($ocont, $ncont, $contmark) = (' ', ' ', '+');
    if (defined($old)) {
	chop($old = &expand($old));
	$old = &num($oline, $old) if $opt_n;
    }
    if (defined($new)) {
	chop($new = &expand($new));
	$new = &num($nline, $new) if $opt_n;
    }
    while (1) {
	($o, $old) = unpack("a${width}a*", $old);
	($n, $new) = unpack("a${width}a*", $new);
	printf $format, $o, $ocont, $mark, $ncont, $n;
	last if (!$opt_f || (!length($old) && !length($new)));
	if ($opt_n) {
	    $old = ' ' x 5 . $old if (length($old));
	    $new = ' ' x 5 . $new if (length($new));
	} else {
	    $ocont = length($old) ? $contmark : ' ';
	    $ncont = length($new) ? $contmark : ' ';
	}
    }
}

sub range {
    local($_) = @_;
    /,/ ? split(/,/) : ($_, $_);
}

sub num {
    local($num, $_) = @_;
    sprintf($nformat, $num, $_);
}

sub expand {
    local($_, $t) = @_;
    if (($test || (($test = '10') =~ s/0/$`/, $test)) eq '11') {
	1 while(s/\t/' ' x (8 - length($`)%8)/e);
    } else {
	while(($t = index($_, "\t")) > $[) {
	    substr($_, $t, 1) = ' ' x (8 - $t % 8);
	}
    }
    $_;
}
