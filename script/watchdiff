#!/usr/bin/env perl

##
## watchdiff: watch difference
##
## Copyright (c) 2014- Kazumasa Utashiro
##
## Original version on Feb 15 2014
##

use strict;
use warnings;
require 5.014;

use Fcntl;
use Pod::Usage;
use Data::Dumper;

use App::sdif;
my $version = $App::sdif::VERSION;

my $opt_d;
my $opt_diff;
my $opt_date = 1;
my $opt_refresh = 1;
my $opt_interval = 2;
my $opt_count = 1000;
my $opt_clear = 1;
my $opt_silent = 0;
my $opt_newline = 1;
my $opt_mark = 0;
my $opt_old = 0;
my @opt_exec;
my @opt_colormap;

my @optargs = (
    "d" => \$opt_d,
    "diff=s" => \$opt_diff,
    "e|exec=s" => \@opt_exec,
    "r|refresh:1" => \$opt_refresh,
    "i|interval=i" => \$opt_interval,
    "c|count=i" => \$opt_count,
    "clear!" => \$opt_clear,
    "s|silent!" => \$opt_silent,
    "M|mark!" => \$opt_mark,
    "O|old!" => \$opt_old,
    "D|date!" => \$opt_date,
    "N|newline!" => \$opt_newline,
    "colormap|cm=s" => \@opt_colormap,
    "p|plain" => sub { $opt_date = $opt_newline = 0 },
    "h|help" => sub { pod2usage() },
    "H|man" => sub { pod2usage( {-verbose => 2} ) },
    );

use Getopt::EX::Long;
Getopt::Long::Configure(qw(bundling require_order));
GetOptions(@optargs) || pod2usage();

my %colormap = qw(
    NEW		K/544
    CHANGE	K/445
    TEXT	K/554E
);

use Getopt::EX::Colormap;
my $cm = Getopt::EX::Colormap
    ->new(HASH => \%colormap)
    ->load_params(@opt_colormap);

my @default_diff = <<"EOS" =~ /\S+/g;
    cdif --no-command --no-unknown -U100
    --cm APPEND=DELETE=$colormap{NEW}
    --cm *CHANGE=$colormap{CHANGE}
    --cm *TEXT=$colormap{TEXT}
EOS
my $default_diff = join ' ', @default_diff;

if (@ARGV) {
    push @opt_exec, [ @ARGV ];
} else {
    @opt_exec or pod2usage();
}

use IO::Handle;
(my $stdout = new IO::Handle)->fdopen(fileno(STDOUT), "w") or die;
sub flush { $stdout->printflush(@_) }

my $diffcmd = do {
    if ($opt_diff) {
	$opt_diff;
    } else {
	join(' ',
	     $default_diff,
	     $opt_mark ? () : qw(--nomark),
	     $opt_old  ? () : qw(--noold),
	    );
    }
};

use App::cdif::Command;
my $old = new App::cdif::Command @opt_exec;
my $new = new App::cdif::Command @opt_exec;

use List::Util qw(pairmap);
use Getopt::EX::Colormap qw(ansi_code);
my %termcap = pairmap { $a => ansi_code($b) } qw(
    home	{CUP}
    clear	{CUP}{ED2}
    el		{EL}
    ed		{ED}
    );

print $termcap{clear} if $opt_refresh;
my $count = 0;
my $refresh_count = 0;
while (1) {
    $old->rewind;
    $new->update;
    my $exec = sprintf "%s %s %s", $diffcmd, $old->path, $new->path;
    my $data = `$exec` // die;
    if ($data eq '') {
	if ($opt_silent) {
	    flush $new->date, "\r";
	    next;
	}
	$data = $new->data;
    }
    $data .= "\n" if $opt_newline;
    if ($opt_refresh) {
	$data =~ s/^/$termcap{el}/mg;
	if ($refresh_count++ % $opt_refresh == 0) {
	    print $termcap{clear};
	}
    }
    print $new->date, "\n\n" if $opt_date;
    print $data;
    if ($opt_refresh and $opt_clear) {
	flush $termcap{ed};
    }
} continue {
    last if ++$count == $opt_count;
    ($old, $new) = ($new, $old);
    sleep $opt_interval;
}

flush $termcap{el} if $opt_refresh;

exit;

######################################################################

=pod

=head1 NAME

watchdiff - repeat command and watch the differences

=head1 SYNOPSIS

watchdiff option -- command

Options:

	-r, --refresh:1     refresh screen count (default 1)
	-i, --interval=i    interval time in second (default 2)
	-c, --count=i       command repeat count (default 1000)
	-e, --exec=s        set executing commands
	-s, --silent        do not show same result
	-p, --plain         shortcut for --nodate --nonewline
	--[no]date          show date at the beginning (default on)
	--[no]newline       print newline result (default on)
	--[no]clear         clear screen after output (default on)
	--diff=command      diff command used to compare result

=head1 EXAMPLES

	watchdiff ifconfig -a

	watchdiff df

	watchdiff --silent df

	watchdiff --refresh 5 --noclear df

	watchdiff -sri1 -- netstat -sp icmp

	watchdiff -e uptime -e iostat -e df

	watchdiff -sr --diff 'sdif --cdif -U100' netstat -S -I en0

	watchdiff -pc18i10r0 date; say tea is ready


=head1 AUTHOR

Kazumasa Utashiro

L<https://github.com/kaz-utashiro/sdif-tools>


=head1 SEE ALSO

L<diff(1)>, L<cdif(1)>, L<sdif(1)>


=cut
